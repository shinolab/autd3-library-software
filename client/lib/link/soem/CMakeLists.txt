cmake_minimum_required(VERSION 3.20)

find_package(Threads REQUIRED)

file(GLOB_RECURSE SOEM_SRC_FILES ${SOEM_PATH}/soem/*.c ${SOEM_PATH}/osal/${OS}/*.c ${SOEM_PATH}/oshw/${OS}/*.c)

add_library(link_soem
  autdsoem.cpp
  autdsoem.hpp
  soem.cpp
  ${SOEM_SRC_FILES}
  ${PROJECT_SOURCE_DIR}/include/autd3/link/soem.hpp
)
target_include_directories(link_soem PUBLIC ${PROJECT_SOURCE_DIR}/include)

target_include_directories(link_soem PRIVATE
  ${PROJECT_SOURCE_DIR}/include
  ${SOEM_PATH}/soem
  ${SOEM_PATH}/osal
  ${SOEM_PATH}/osal/${OS}
  ${SOEM_PATH}/oshw/${OS}
)
if(WIN32)
  target_include_directories(link_soem PRIVATE
    ${SOEM_PATH}/oshw/${OS}/wpcap/Include
    ${SOEM_PATH}/oshw/${OS}/wpcap/Include/pcap
  )
  target_compile_options(link_soem PRIVATE /D_CRT_SECURE_NO_WARNINGS /wd4996)
endif()

target_link_libraries(link_soem INTERFACE ${CMAKE_THREAD_LIBS_INIT})
if(WIN32)
  if("${CMAKE_VS_PLATFORM_NAME}" STREQUAL "Win32")
    target_link_directories(link_soem INTERFACE ${PROJECT_SOURCE_DIR}/lib/link/soem/Lib)
  else()
    target_link_directories(link_soem INTERFACE ${PROJECT_SOURCE_DIR}/lib/link/soem/Lib/x64)
  endif()
  target_link_libraries(link_soem INTERFACE ws2_32.lib winmm.lib Packet.lib wpcap.lib)
elseif(APPLE)
  target_link_libraries(link_soem INTERFACE pcap)
else()
  target_link_libraries(link_soem INTERFACE rt -static-libgcc -static-libstdc++)
endif()

set_target_properties(link_soem
  PROPERTIES
  ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib"
  LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib"
  RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib"
)

if(WIN32)
  set_target_properties(link_soem PROPERTIES FOLDER "link")
else()
  set_target_properties(link_soem PROPERTIES POSITION_INDEPENDENT_CODE ON)
endif()
